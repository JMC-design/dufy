;;; This is a script file which generates fundamental data and saves them as a .lisp file.

(eval-when (:compile-toplevel :load-toplevel :execute)
  (asdf:load-system :alexandria)
  (asdf:load-system :dufy-internal))

(use-package :dufy-internal)

(defparameter this-dir-path (uiop:pathname-directory-pathname *load-pathname*))
(defparameter obj-name "y-to-value-data.lisp")
(defparameter obj-path (merge-pathnames obj-name this-dir-path))


;; convert munsell value to Y in [0, 1]
(defun munsell-value-to-y (v)
  (* v (+ 1.1914d0 (* v (+ -0.22533d0 (* v (+ 0.23352d0 (* v (+ -0.020484d0 (* v 0.00081939d0)))))))) 0.01d0))

(defun find-root (func rhs min max threshold)
  "bisection method"
  (let* ((mid (* 0.5d0 (+ min max)))
         (lhs (funcall func mid))
         (delta (abs (- lhs rhs))))
    (if (<= delta threshold)
        mid
        (if (> lhs rhs)
            (find-root func rhs min mid threshold)
            (find-root func rhs mid max threshold)))))

(defparameter y-to-munsell-value-arr
  (make-array 1001 :element-type 'double-float :initial-element 0.0d0))

(setf (aref y-to-munsell-value-arr 0) 0.0d0)
(setf (aref y-to-munsell-value-arr 1000) 10.0d0)
(loop for y from 1 to 999
      do (setf (aref y-to-munsell-value-arr y)
               (find-root #'munsell-value-to-y (* y 0.001d0) 0 10 1.0d-6)))

;; For test in devel. Y should be in [0,1].
(defun y-to-munsell-value (y)
  (let* ((y1000 (* (alexandria:clamp y 0 1) 1000))
         (y1 (floor y1000))
         (y2 (ceiling y1000)))
    (if (= y1 y2)
        (aref y-to-munsell-value-arr y1)
        (let ((r (- y1000 y1)))
          (+ (* (- 1 r) (aref y-to-munsell-value-arr y1))
             (* r (aref y-to-munsell-value-arr y2)))))))


(with-open-file (out obj-path
                     :direction :output
                     :if-exists :supersede)
  (format out ";;; This file is automatically generated by ~a.~%~%"
          (file-namestring *load-pathname*))
  (format out "(in-package :dufy-munsell)~%~%")
  (print-make-array "y-to-munsell-value-arr" y-to-munsell-value-arr out))

(format t "The file is saved at ~A~%" obj-path)
